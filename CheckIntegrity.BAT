@ECHO OFF
 rem ==========================================================================
 rem === Assess and Validate File/Folder Integrity
 rem ==========================================================================
 rem
 rem Created On: 08 Oct 2014
 rem         By: Andrew S. Baker
 rem
 rem Updated On: 29 Sep 2025 / 18 Sep 2025 / 01 Sep 2025 / 31 Aug 2025
 rem         By: Andrew S. Baker
 rem


 rem ==========================================================================
 rem  OS Required: Windows 2008 or later (previously Windows 2003+)
 rem
 rem  Non-Native Utilities Used/Required:
 rem    BrainWave Utilities .... https://www.BrainWaveCC.com/brainwave-utilities
 rem
 rem    AUTORUNSC .............. https://learn.microsoft.com/en-us/sysinternals/downloads/autoruns
 rem    PSTOOLS ................ https://learn.microsoft.com/en-us/sysinternals/downloads/pstools
 rem    Blat ................... http://www.blat.net/
 rem    DIFF ................... http://gnuwin32.sourceforge.net/packages/diffutils.htm
 rem    InfoZip Utilities ...... http://sourceforge.net/projects/infozip/files/
 rem    MiTec System Info X .... https://www.mitec.cz/msi.html
 rem    SysLogGen .............. http://www.snmpsoft.com/freetools/sysloggen.html
 rem
 rem    AuditPol ............... Resource Kit (Native in Vista+)
 rem    DRIVERQUERY ............ Native in Windows XP/2003/Vista+
 rem    SYSTEMINFO ............. Native in Windows XP/2003/Vista+
 rem    WMIC ................... Native in Windows XP/2003/Vista+  (Deprecated in Server 2025+)
 rem    Resource Kit ........... https://en.wikipedia.org/wiki/Resource_Kit
 rem
 rem  Input Files Used By Script (stored in %SystemDrive%\Scripts\Bat\Input):
 rem    CustomVariables.TXT .... Centralized Configuration Settings for Scripts
 rem
 rem  Required Scripts:
 rem    SetDrive.BAT ........... Set Global Variables for this Environment
 rem ==========================================================================

 :::  This script obtains the file hashes for all executables and scripts on the
 :::  local system (including .EXE .DLL .COM .OCX .SYS .BAT .CMD .KIX .PS1 .JAVA
 :::  and more) and compares them to the previous hash database generated by the
 :::  script. It will send an email with the summary log of the findings, if
 :::  there are any changes.

 :::
 :::  All activities are logged to the default syslog server(s) configured for
 :::  the system.  Emails are also sent upon the initial run of the script, or
 :::  whenever there are changes to the watched files on a drive.
 :::
 :::  You can use one or more of the @EXCLUDE family of variables (via the
 :::  CustomVariables.TXT config file) to prevent specific drives on multiple
 :::  systems from being scanned.  Here is what these variables are used for:
 :::
 :::   -- @EXCLUDE .................. Exclude multiple drives on all systems
 :::   -- @EXCLUDE_<driveletter> .... Exclude named drive on multiple systems
 :::   -- @EXC_<computername> ....... Exclude multiple drives on named system
 :::
 :::  (Oct 2014) v1.2 -- The drives which are to be scanned, are now saved as
 :::  a parameter of the scheduled job.
 :::
 :::  (Oct 2014) v1.4 -- The list of file/folder scan exemptions have been
 :::  consolidated into a single file in the INPUT folder: ScanExemption.TXT
 :::
 :::  (Oct 2014) v1.5 -- Introduced the new BrainWave ChangeCase utility in
 :::  order to take advantage of FCIV's -type parameter, since, unfortunately,
 :::  the -type parameter can only perform case-sensitive matches on file
 :::  extensions.
 :::
 :::  NOTE: The @EXE2SCAN and @SCR2SCAN variables cannot contain more than 5
 :::        or 6 extensions, or the FCIV command string will become too long
 :::        after being converted to upper, lower, and mixed case by the
 :::        BrainWave ChangeCase utility.
 :::
 :::  (Oct 2014) v1.6 -- Improved error logging and status reporting. The
 :::  PSINFO utility was replaced by the COMPINFO utility for increased
 :::  performance and reduced complexity.
 :::
 :::  (Oct 2014) v2.0 -- File hashes are now being generated by FSUM instead of
 :::  the FCIV utility. This has greatly improved performance, and also reduced
 :::  complexity. Due to this change, the BrainWave ChangeCase utility is no
 :::  longer needed in this script.
 :::
 :::  (Oct 2014) v2.1 -- Streamlined file and folder exemption processing, and
 :::  added a routine for consolidating output to logs and the console.
 :::
 :::  (Nov 2014) v2.2 -- Revamped the listing of modified files in the job
 :::  report, and added better maintenance for temporary files. Additionally,
 :::  the new BrainWave MakeString utility provides an alternative to the
 :::  @DIVIDER variables.
 :::
 :::  (Nov 2014) v2.3 -- Log files are now compressed before emailing, in
 :::  order to reduce attachment size.
 :::
 :::  (Nov 2014) v2.4 -- The ScanExemption.TXT input file has been deprecated.
 :::  The global @DONTSCAN variable will control file/folder exclusions for
 :::  all drives, unless one or more a @DONTSCAN-x variables are found, where
 :::: "x" is the a drive letter that needs to deviate from the global setting.
 :::
 :::  (Nov 2014) v2.5 -- Specific files or folders (e.g. Windows32) can now be
 :::  watched via the @WATCHFOR variable.  Changes to these files/folders are
 :::  also recorded in a special MONITOR log, and summarized in the main log.
 :::
 :::  (Dec 2014) v2.6 -- The FNR (File and Replace utility) is now used to
 :::  clean up the initial hash database so that it no longer contains any
 :::  access denied or file not found lines.
 :::
 :::  (Jan 2015) v2.8 -- Reduced email report to final summary info, only.
 :::  Used new logfile management routine to simplify log generation for
 :::  external scripts such as OpsLogs.BAT and SecurityLog.BAT.  Added an
 :::  option (@MAIL="WATCH") to send mail only if there are changes to
 :::  monitored files.
 :::
 :::  (Mar 2015) v2.9 -- Delete any drive evaluation that is older than
 :::  @OBSOLETE days old (default = 100).  This is to clean out comparisons
 :::  for old or disconnected drives.  Also swapped the role of the @ and $
 :::  parameters to be more consistent with other scripts.
 :::
 :::  (Mar 2015) v2.10 -- Track elapsed time of integrity scans using a new
 :::  version of DATEINFO
 :::
 :::  (Nov 2015) v2.12 -- Changed output format of history log
 :::
 :::  (Nov 2015) v2.13 -- Removed DNS error output from the Syslog routines.
 :::
 :::  (May 2016) v3.0 -- Added routines to obtain and compare Audit Policy
 :::  Setting; Modified script version number format to include build number.
 :::
 :::  (Mar 2017) v4.0 -- Now generates SHA2_256 hashes in place of earlier
 :::  MD5 hashes; Corrected errors in flag processing that were introduced in
 :::  v3.0; ChangeCase is used to maintain all drive variables in UpperCase,
 :::  and duplicate drive entries are now removed from the inclusion/exclusion
 :::  lists.
 :::
 :::  (Nov 2017) v4.1 -- Added --silent parameter to FNR text replacement
 :::
 :::  (Jan 2018) v4.2 -- Changed the HELP routine to use FINDSTR;
 :::  Fixed a new issue with using FINDSTR /R /C in checking for scheduled jobs;
 :::  Migrated :SaveJobHistory to a subroutine, for additional flexibility;
 :::  Invalid job attempts are now recorded in the .RECORDING.TXT file.
 :::
 :::  (Feb 2018) v4.3 -- Moved the CMD filtering to its own routine, and added
 :::  HELP {/?} filtering
 :::
 :::  (Mar 2018) v5.0 -- The SYNTAX HELP routine has been updated;
 :::  -- The release of FILEHASH v2.x provides the following functionality:
 :::  traverse folders natively; control the display of filenames with or
 :::  without full/base path; perform file exclusions inline; display the size
 :::  of hashed files; provide an FSUM-compatible display mode;
 :::  -- FILEHASH is now the preferred hash utility for the entire script
 :::  repository, and routines which use it have been upgraded accordingly;
 :::  -- Support for both MD5SUM and FCIV have been eliminated from this script;
 :::  -- Support for NeoLogger has been eliminated from this script.
 :::
 :::  (Apr 2018) v5.1 -- FILEHASH v2.3 can now write directly to a text file;
 :::  -- Removed support for FSUM, which has greatly simplified the script;
 :::  -- Have *almost* eliminated the need for FNR as well, but a new version
 :::     of FNR corrected that annoying CMD window spawning issue;
 :::  -- Changed Script Documentation to www.BrainWaveCC.com
 :::
 :::  (Jul 2018) v5.2 -- The new BrainWave CheckParams utility is now used to
 :::  check for the /H and /? parameters, replacing the use of FIND/FINDSTR;
 :::
 :::  (Aug 2018) v6.0 -- Removed AT command support;
 :::  -- FileHash64.exe will be called in place of FileHash.exe on Windows x64
 :::  -- Files larger than @MAX_FILE_SIZE (in bytes) will not be placed in the
 :::     hash database by the script;
 :::
 :::  (Oct 2018) v6.1 -- Corrected and expanded the DEBUG logging routine;
 :::
 :::  (Nov 2018) v6.2 -- Corrected the false positives with DB HASH UPGRADE warnings;
 :::
 :::  (Nov 2018) v7.0 -- Added integrity verification of the BIOS and other System
 :::  Configuration elements, using SYSTEMINFO, COMPINFO, WMIC, other native Windows
 :::  utilities, and a few utilities from Microsoft's SysInternals repository;
 :::  --- The DB HASH UPGRADE warning routines have been refactored, relying on
 :::  the recently released FileHash v2.7;
 :::
 :::  (Jan 2019) v7.1 -- Reduced the default amount of System Configuration info
 :::  captured, and added the @EXTENDED_CONFIG variable to enable the capturing
 :::  of additional information for comparison;
 :::  -- @EXTENDED_CONFIG can be set with the new "~" parameter, or within the
 :::  CustomVariables* configuration file;
 :::
 :::  (Mar 2019) v8.0 -- On 64-bit Windows, the x64 utilities folder is placed
 :::  ahead of the 32-bit only utilities in the PATH, favoring the execution of
 :::  the 64-bit edition of FileHash.exe over the 32-bit edition;
 :::  -- Script will exit if FILEHASH executable is not available;
 :::  -- Record additional attributes in the Job History log;
 :::  -- Prevent DEBUG MODE data from being stored in the .RECORDING.TXT file
 :::  -- Enabled Code Page 1252 Support;
 :::
 :::  (Oct 2019) v8.1 -- Updated Job History Output;
 ::: -- Updated :ShowStatus routine to reset TITLE/CodePage;
 :::
 :::  (Oct 2019) v8.2 -- Update :PadLeft and :PadRight routines;
 :::
 :::  (Dec 2019) v8.3 -- New method to determine if 64-bit FILEHASH is running;
 :::
 :::  (Dec 2019) v8.4 -- Updated :ShowStatus routine to use BrainWave NOW.exe;
 :::
 :::  (Jan 2020) v8.5 -- Use CHECKPARAMS to evaluate parameters besides /? /H;
 :::  -- Added a routine for validating integer parameters;
 :::  -- Refactored the :Prepare4Syslog routine;
 :::  -- Consolidate :PadLeft and :PadRight routines;
 :::
 :::  (May 2020) v9.0 -- Migrated :DebugMode routine to SetDrive.BAT;
 :::  -- Replaced FINDSTR with BrainWave ReadConfig.exe for reading config file;
 :::  -- Added support for more hash algorithms including BLAKE3 and XXHash64;
 :::  -- Changed fallback hash, if none supplied, from SHA256 to XXHASH64;
 :::
 :::  (Nov 2020) v9.1 -- Provided extended HELP options (--?? / --H2);
 :::  -- New CHECKPARAMS syntax for processing extended help options;
 :::  -- Fixed a minor issue with the Filtered CMD Title routine;
 :::
 :::  (Mar 2021) v9.2 -- Refactored routines for Add/Change/Delete reporting;
 :::
 :::  (Jul 2021) v9.3 -- Replaced some COMPINFO with PSINFO to avoid intermittent crashes;
 :::
 :::  (Jan 2022) v9.4 -- Eliminated the use of FNR with BrainWave Readable v2.0;
 :::  -- Add a suffix to @WATCHFOR variable to avoid FINDSTR parsing error;
 :::  -- Modify the @NAMECHK verification to include \ prefix;
 :::
 :::  (Oct 2023) v9.5 -- Change the way the @SCRIPT_BEG# variable is
 :::  calculated in various subroutines by using DATEINFO instead of %DATE%;
 :::
 :::  (Apr 2024) v9.6 -- Check for essential utilities with FINDFILES;
 :::
 :::  (May 2024) v9.7 -- Add logging when (un)scheduling jobs;
 :::
 :::  (Jul 2024) v9.8 -- Replaced the :RunCMD routine with just a CALL command;
 :::
 :::  (Sep 2024) v9.9 -- Enable updates to local syslogs if requested;
 :::  -- Refactoring of the scheduled jobs verification routine;
 :::  -- Created :LogCriticalFailure routine for early job failures;
 :::
 :::  (Feb 2025) v10.0 -- Replaced COMPINFO with MiTec System Info X
 :::  to get around the constant process crashes of COMPINFO;
 :::
 :::  (Aug 2025) v10.1 -- Generate hashes of zipped integrity logs to email;
 :::  -- Try to install the now deprecated WMIC via DISM if not found in path;
 :::
 :::  (Sep 2025) v10.2 -- Replaced instances of HEAD/TAIL/TYPE with READABLE;
 :::  -- Replaced instances of FORFILES with FILEHASH --JF;
 :::
 ::: --------
 :::  NOTES:
 ::: --------
 :::  * Despite the use of doublequotes with the @WATCHFOR variable, leading
 :::    and trailing spaces will still be trimmed during FINDSTR processing;
 :::
 :::
 :::  SCRIPTING SYNTAX RESOURCES:
 :::  -------------------------------------------------------------------------
 :::  https://ss64.com/ .......... CMD and Powershell syntax
 :::  https://www.dostips.com/ ... Windows Shell Scripting Tips
 :::  -------------------------------------------------------------------------


 rem ==========================================================================
 rem === Generate Filtered CMD Title
 rem === Updated On: 14 Aug 2025 / 25 Nov 2020 / 25 Feb 2018
 rem ==========================================================================
:DisplayTitle
 SET "@TTT=%* "
 SET "@TTT=%@TTT:/?={HELP}%"
 TITLE [%USERDOMAIN%\%USERNAME%] - %~f0 %@TTT% >NUL 2>NUL
 SET "@TTT="


 rem ==========================================================================
 rem === Initialize Environment Variables
 rem === Updated On: 29 Sep 2025 / 18 Sep 2025 / 01 Sep 2025 / 31 Aug 2025 / 01 Feb 2025
 rem ==========================================================================
:Variables
 SETLOCAL ENABLEDELAYEDEXPANSION & CALL :ShowStatus "STARTED" v10.2.0.1020 k

 rem -- Display Syntax Help if Required/Requested
 CHECKPARAMS --rc -c "H HELP ?" -x "?? H2" -s %*
 IF ERRORLEVEL 1 IF NOT ERRORLEVEL 16 GOTO :HelpMessage

 rem -- Check for Essential Utilities and/or Scripts
 SET #BWUTILS="%~dp0SetDrive.BAT" CHANGECASE.EXE FILEHASH.EXE MAKESTRING.EXE READABLE.EXE SUBSTRING.EXE
 SET #MSUTILS=AUDITPOL.EXE AUTORUNSC.EXE DRIVERQUERY.EXE FINDSTR.EXE SCHTASKS.EXE SORT.EXE SYSTEMINFO.EXE
 SET #O_UTILS=BLAT.EXE DIFF.EXE GETSYS.EXE SYSLOGGEN.EXE ZIP.EXE
 FINDFILES --bw -w -m -f %#BWUTILS% %#MSUTILS% %#O_UTILS% -o WMIC.EXE DISM.EXE
 IF ERRORLEVEL 64 (CALL :LogCriticalFailure ABORTED - MISSING CRITICAL FILES & GOTO :ExitBatch)

 rem -- Default Variables (Override via CustomVariables.*.TXT)
 SET @MAIL=FALSE
 SET @MAILPORT=25
 SET @MAILSERVER=smtp.domain.tld
 SET @MAILSENDER=LogAdmin@domain.tld
 SET @RCPT-TO=Ops@Domain.tld
 SET @ALERT-TO=Ops@Domain.tld
 SET @WATCHFOR="\System32" "\SysWOW64" "\Installer\" "\assembly\Native"
 SET @DONTSCAN="Backups\Scripts\TestBackup" "Backups\TestBackup" "data\application" "dropbox.cache" "dropbox.attr" "pagefile.sys" "swapfile.sys" "hiberfil.sys"
 SET @EXE2SCAN=.EXE .COM .MSI .MSU .PYD .OCX .DLL .SYS
 SET @SCR2SCAN=.BAT .CMD .KIX .PS1 .VBS .PL .PY .PYC .PYX .PHP .JAVA .SQL
 SET @CFG2SCAN=.config hosts
 SET @MAX_FILE_SIZE=500,000,000
 SET @EXTENDED_CONFIG=
 SET @NOACCESS=:::DENIED:::

 rem -- Default Hash Values
 SET @HASHOPT=SHA256
 SET @FALLBACK_HASH=XXHASH64
 SET @LOG_HASH=BLAKE3
 SET @HASH_MATRIX="MD5:32:"":+"  "SHA1:40"  "SHA2 SHA2_256:64:SHA256"  "SHA2_384:96:SHA384"  "SHA2_512:128:SHA512"  "SHA3:64:SHA3_256"  "SHA3_384:96"  "SHA3_512 BLAKE2B:128"  "BLAKE2S BLAKE3:64"  "XX64:16:XXHASH64"  "MH3 MURMURHASH3 MURMURHASH3_128 MURMURHASH3_X64:32:MURMURHASH3_X64_128"  "MURMURHASH3_X86:32:MURMURHASH3_X86_128"

 rem -- Main Variables
 SET @KEEP_HISTORICAL=7
 SET @OBSOLETE=100
 SET @SKIPDRIVES=
 SET @VALIDDRIVES=
 SET @DRIVES2SCAN=%*
 SET @DRIVES2SCAN=!@DRIVES2SCAN:#:=#=!
 SET @AUTOSELECT=FALSE& IF "%*"=="" SET @AUTOSELECT=TRUE
 SET @FLAG_EXTENSIONS=ENC ZZZ
 SET @TEMP_EXTENSIONS=CHG ADD DEL DIF LOG TMP
 FOR %%V IN (SCAN CHG ADD DEL) DO SET @TOTAL_%%V=0

 rem -- Job Scheduling Variables
 SET @JOBNAME=Track File Integrity
 SET @JOBUSER=SYSTEM
 SET @JOBFREQ=DAILY
 SET @JOBTIME=00:30:00
 SET @JOBDATE=
 SET @JOBPATH=
 SET @JOBVRFY=
 SET @JOBTYPE=

 rem -- Set Global Variables from Centralized Script
 CALL "%~dp0SetDrive.BAT" "%~n0"

 rem -- Ensure Valid Log Processing Values
 CALL :GetInteger @MINROWS "%@MIN_LOG_ROWS%" 005 001 010
 CALL :GetInteger @MAXROWS "%@MAX_LOG_ROWS%" 199 030 399

 rem -- Validate Legitimate File Size Exemptions for FILEHASH processing
 CALL :GetInteger @MAX_SIZE "%@MAX_FILE_SIZE%" 500000000 5000 9999999999

 rem -- Process Special Command-line Parameters
 SET @ALL_PARAMS=%*& CALL :SwapCharacters @ALL_PARAMS "#=" "#:"
 CALL :GetParams "#" @HASHOPT & IF DEFINED @OK IF NOT DEFINED @HASHOPT (GOTO :HelpMessage)
 CALL :GetParams "@" & IF DEFINED @OK (SET @NOSCHED=TRUE)
 CALL :GetParams "-" & IF DEFINED @OK (SET @SCHEDULEONLY=TRUE& SET @UNSCHED=TRUE)
 CALL :GetParams "." & IF DEFINED @OK (SET @SCHEDULEONLY=TRUE& SET @UPDATESCHED=TRUE)
 CALL :GetParams "$" & IF DEFINED @OK (SET @AUTOSELECT=TRUE)
 CALL :GetParams "+" & IF DEFINED @OK (SET @MAILIT=TRUE)
 CALL :GetParams "~" & IF DEFINED @OK (SET @EXTENDED_CONFIG=TRUE)

 rem -- Determine Valid Hash and Set Associated Variables
 CALL :ValidateHash @HASHOPT  @SORTLEN @MARKER
 CALL :ValidateHash @LOG_HASH @V1 @V2

 rem -- Permanent Variables (Don't Override via CustomVariables.*.TXT)
 SET @CHANGES=N
 SET @WATCHCOUNT=0
 SET @CONFIG_CHANGES=0
 SET @POLICY_CHANGES=0

 rem -- Determine Whether x64 or x86 Edition of FileHash is Available
 FOR /F "TOKENS=3" %%X IN ('FILEHASH --Version-Only 2^>^&1') DO IF /I "%%~X"=="x64" (
	 SET @x64_MSG=64-bit Mode
	 SET @x64_MODE= // !@x64_MSG!
 )

 rem -- Protect the @WATCHFOR variable by adding a bogus suffix
 SET @WATCHFOR=%@WATCHFOR% ~~~

 rem -- Generate Scan Selection Variables
 SET @EXT2SCAN=%@EXE2SCAN% %@SCR2SCAN% %@CFG2SCAN%
 SET @INCLUDE=& FOR %%T IN (%@EXT2SCAN%) DO SET @INCLUDE=!@INCLUDE! *%%~T

 rem -- Standard Log Variables
 SET @ZIPPATH=%@STORAGE%\Logs\ZipFiles
 SET @ARCHIVE=%@ZIPPATH%\%COMPUTERNAME%-%~n0.%@ZIPSTAMP%
 SET @LOGPATH=%@STORAGE%\Logs\Audit
 SET @LFORMAT=%@LOGPATH%\%~n0
 SET @TIMINGS=%@LFORMAT%.RECORDING.TXT
 SET @LOGFILE=%@LFORMAT%.%@FILESTAMP%
 FOR %%L IN (SYSINFO MONITOR REPORT SUMMARY TEMPLOG ERRORS CHECKSUM) DO SET @%%L=%@LFORMAT%.%%L.%@FILESTAMP%
 SET @LOGLIST="%@LOGFILE%" "%@SYSINFO%" "%@MONITOR%" "%@REPORT%" "%@ERRORS%"
 SET @CLEANUP="%@TEMPLOG%" "%@SUMMARY%" "%@MONITOR%" "%@REPORT%" "%@ERRORS%"

 rem -- Key Variable Verification
 FOR %%V IN (ALL_PARAMS FILEHASH EXT2SCAN WATCHFOR INCLUDE KEEP_HISTORICAL) DO IF NOT DEFINED @%%~V (
	 ECHO:
	 ECHO *** WARNING: The @%%~V variable is undefined.
	 ECHO:
	 ECHO  Please ensure that all configuration info is provided correctly
	 ECHO  within the file: "%@CUSTOM-VARS%"
	 ECHO:
	 ECHO  Alternatively, this information can be entered directly into this script,
	 ECHO  although this is not recommended because it undermines portability.
	 ECHO %@DIVIDER*%
	 ECHO:
	 CALL :SaveJobHistory ABORTED - UNDEFINED VARIABLE
	 GOTO :HelpMessage
 )
 %@PRINT_IF_DEBUG%


 rem ==========================================================================
 rem === Attempt to install WMIC if not found in Windows 11 / Server 2025
 rem === Updated On: 31 Aug 2025
 rem ==========================================================================
:GetWMIC
 FINDFILES --RC -F WMIC.EXE || DISM /Online /Add-Capability /CapabilityName:WMIC
 %@PRINT_IF_DEBUG%


 rem ==========================================================================
 rem === Use PSINFO To Determine Local Fixed Drive Letters
 rem === Updated On: 18 Aug 2025 / 28 Jul 2024 / 14 Jul 2021 / 27 Nov 2018
 rem ==========================================================================
:GetDrivesToScan
 FOR %%F IN ("%@LOGPATH%" "%@ZIPPATH%") DO IF NOT EXIST "%%~F" MD "%%~F"

 rem -- Obtain All Possible Local Drives
 CALL :WriteLn "IDENTIFY VALID DRIVES..." >NUL
 FOR /F "TOKENS=1-2" %%L IN ('PSINFO %@PSPARAMS% -d 2^>NUL ^| FINDSTR /R /C:"    [A-Z]: "') DO IF "%%~M"=="Fixed" (
	 SET @VALIDDRIVES=!@VALIDDRIVES! %%~L
 ) ELSE (
	 SET @SKIPDRIVES=!@SKIPDRIVES! %%~L
 )
 IF /I "%@AUTOSELECT%"=="TRUE" SET @DRIVES2SCAN=!@VALIDDRIVES!

 rem -- Ensure Valid Drive Letters
 FOR %%E IN (%@DRIVES2SCAN%) DO (
	 SET @LD=%%~E
	 SET @LD=!@LD:~0,2!
	 IF "!@LD:~-1!"==":" (
		 IF NOT "!@LD!"=="%%~E" CALL SET @DRIVES2SCAN=!@DRIVES2SCAN:%%~E=%%@LD%%!
	 ) ELSE (
		 SET @DRIVES2SCAN=!@DRIVES2SCAN:%%~E=!
	 )
 )

 rem -- Process @EXCLUDE_driveletter Exclusions
 FOR /F "TOKENS=1-3 DELIMS=_=" %%V IN ('SET "@EXCLUDE_" 2^>NUL') DO (
	 FOR %%C IN (!@EXCLUDE_%%~W!) DO IF /I "%%~C"=="%COMPUTERNAME%" (
		 SET @SKIPDRIVES=%%~W: !@SKIPDRIVES!
		 SET @DRIVES2SCAN=!@DRIVES2SCAN:%%~W:=!
	 )
 )

 rem -- Process other @EXCLUDE Exclusions
 FOR %%E IN (%@EXCLUDE% !@EXC_%COMPUTERNAME%!) DO (
	 SET @SKIPDRIVES=%%~E !@SKIPDRIVES!
	 SET @DRIVES2SCAN=!@DRIVES2SCAN:%%~E=!
 )

 rem -- Cleanup the @DRIVES2SCAN, @VALIDDRIVES and @SKIPDRIVES variables
 FOR %%V IN (@DRIVES2SCAN @SKIPDRIVES @VALIDDRIVES) DO IF DEFINED %%~V (
	 SET %%V=!%%V: =!
	 SET %%V=!%%V::=: !
	 CALL :UpperCase %%V
	 CALL :EnsureUnique %%V
 )

 rem -- Set Final Values for Job Schedulers
 IF "%@DRIVES2SCAN%"=="" SET @DRIVES2SCAN=%SystemDrive%
 FOR %%P IN ("" "$") DO IF "%~1"=="%%~P" (
	 SET @JOBPATH=%~f0 $
	 SET @JOBVRFY=$
 ) ELSE (
	 SET @JOBPATH="%~f0" %@DRIVES2SCAN%
	 SET @JOBVRFY=%@DRIVES2SCAN%
 )

 rem -- Determine # of Drives to be Scanned
 SET @D2S_COUNT=0& FOR %%C IN (!@DRIVES2SCAN!) DO SET /A @D2S_COUNT+=1

 rem -- Generate Scan Exclusion Lists for Each Drive
 SET @DRV_LETTERS=!@DRIVES2SCAN::=!
 SET @CUSTOM_EXC=& FOR %%D IN (!@DRV_LETTERS!) DO IF DEFINED @DONTSCAN-%%D (SET @CUSTOM_EXC=TRUE) ELSE (SET @DONTSCAN-%%D=!@DONTSCAN!)
 %@PRINT_IF_DEBUG%


 rem ==========================================================================
 rem === Create Scheduled Job using SCHTASKS (XP/2003/Vista/+)
 rem === Updated On: 10 May 2024 / 22 Jan 2022 / 09 Mar 2019
 rem ==========================================================================
:ScheduleJobs
 IF /I "%@NOSCHED%"=="TRUE" IF /I NOT "%@UNSCHED%"=="TRUE" (
	 SET @JOBTYPE= // ONE-TIME
	 ECHO:
	 ECHO *** Job Scheduling Disabled ***
	 ECHO:
	 GOTO :ScheduleJobs_END
 )

 ECHO:
 ECHO Scheduling "%@JOBNAME%" on %COMPUTERNAME%...
 ECHO:

 IF %@OSBUILD% LSS 6000 SET @TASKFOLDER=
 SET @NAMECHK=%@JOBNAME:[=\[%
 SET @NAMECHK=!@NAMECHK:]=\]!
 SET @CHK_SCHED=SCHTASKS /QUERY /V /FO LIST
 SET @SET_SCHED=SCHTASKS /CREATE /TN "!@TASKFOLDER!%@JOBNAME%" /TR "%@JOBPATH%" /RU "%@JOBUSER%" /SC %@JOBFREQ% %@JOBDATE% /ST %@JOBTIME% %@JOBLEVEL% %@FORCE%

 rem -- Delete Scheduled Task from Old Custom Folders on Vista/2008+
 IF %@OSBUILD% GEQ 6000 (
	 FOR /F "TOKENS=1* DELIMS=\ " %%s IN ('%@CHK_SCHED% 2^>NUL ^| FIND "TaskName:" ^| FINDSTR /I /R /C:"\\%@NAMECHK%$"') DO (
		 IF /I NOT "%@TASKFOLDER%%@JOBNAME%"=="%%t" SCHTASKS /DELETE /TN "%%t" /F >NUL 2>&1
	 )
 )

 rem -- Check to Remove Scheduled Job
 IF /I "%@UNSCHED%"=="TRUE" (
	 SCHTASKS /DELETE /TN "!@TASKFOLDER!%@JOBNAME%" %@FORCE% 2>NUL
	 ECHO Task has been UNscheduled.  Now exiting script...
	 CALL :SaveJobHistory JOB UNSCHEDULED ONLY
	 GOTO :ExitBatch
 )

 rem -- Check for Schedule Update Parameters
 IF /I "%@UPDATESCHED%"=="TRUE" (!@SET_SCHED!) ELSE (CALL :CheckSched "%~f0" " %@JOBVRFY%$")
 ECHO %@DIVIDER-%
 ECHO:
 IF /I "%@SCHEDULEONLY%"=="TRUE" (
	 ECHO Task has been scheduled.  Now exiting script...
	 IF %@OSBUILD% LSS 6000 (SCHTASKS) ELSE (%@CHK_SCHED% /TN "!@TASKFOLDER!%@JOBNAME%")
	 CALL :SaveJobHistory JOB SCHEDULED ONLY
	 GOTO :ExitBatch
 )
:ScheduleJobs_END
 %@PAUSE_IF_DEBUG%


 rem ==========================================================================
 rem === Obtain Current System Configuration Info
 rem === Updated On: 01 Feb 2025 / 24 Jan 2019 / 27 Nov 2018
 rem ==========================================================================
:GetSystemInfo
 CALL :WriteLn "GENERATE SYSTEM CONFIG..." >NUL

 ( rem -- Write to Log
	 CALL :ShowHeader ECHO %COMPUTERNAME% - [!DATE! at !TIME: =0!] - %@VER%
	 CALL :GetWMIC    BIOS LIST FULL
	 CALL :GetWMIC    VOLUME GET "Drive Letter", "Capacity", "DriveType", "File System", "Label", "Name"

	 IF /I "!@EXTENDED_CONFIG!"=="TRUE" (
		 CALL :GetConfig  SYSTEMINFO
		 CALL :GetConfig  GETSYS /so /ld /na /la /id /usb
		 CALL :GetConfig  NET CONFIG SERVER
		 CALL :GetConfig  NET CONFIG WORKSTATION
		 CALL :GetConfig  NET SHARE
		 CALL :GetWMIC    SHARE LIST FULL
		 CALL :GetConfig  IPCONFIG /ALL
		 CALL :GetConfig  ROUTE -P PRINT
		 CALL :GetConfig  DRIVERQUERY
		 CALL :GetConfig  DRIVERQUERY /SI
		 CALL :GetConfig  DRIVERQUERY /FO TABLE /V
		 CALL :GetConfig2 AUTORUNSC !@PSPARAMS! -a * -o "%@TEMPLOG%"
	 ) ELSE (
		 SYSTEMINFO | FINDSTR /I /R "^Host ^OS ^Registered ^Product ^Original ^System.[M,T] ^Processor Stepping ^BIOS ^Boot Directory: Locale: Zone: ^Total ^Page Domain: "
	 )
 ) >"%@SYSINFO%" 2>&1
 %@PAUSE_IF_DEBUG%


 rem ==========================================================================
 rem === Generate Log Header
 rem === Updated On: 18 Sep 2025 / 30 Aug 2024 / 03 Oct 2019 / 08 Mar 2019
 rem ==========================================================================
:GenerateHeader
 SET @EXCLUDED=*NONE*& IF DEFINED @SKIPDRIVES SET @EXCLUDED=%@SKIPDRIVES%
 SET @DRV_EXC=& IF DEFINED @EXCLUDED SET @DRV_EXC=!@EXCLUDED::=!

 ( rem -- Write to Log
	 ECHO %@DIVIDER*%
	 ECHO *** %COMPUTERNAME% - [!DATE! at !TIME!] - %@VER%
	 ECHO *** Job Name ............... %@JOBNAME% [using %@HASHOPT% Hashes]
	 ECHO *** Job Schedule ........... %@JOBFREQ% at %@JOBTIME%  %@JOBDATE%
	 ECHO *** All Valid Drives ....... %@VALIDDRIVES%
	 ECHO *** Drives to Scan ......... %@DRIVES2SCAN%
	 ECHO *** Excluded Drives ........ %@EXCLUDED%
	 ECHO %@DIVIDER*%
	 ECHO:
	 ECHO FILES TO INCLUDE IN SCAN:
	 ECHO  ---%@INCLUDE%
	 ECHO:
	 ECHO FILES/FOLDERS TO EXCLUDE FROM SCAN:
	 IF DEFINED @CUSTOM_EXC (
		 FOR %%S IN (%@DRV_LETTERS%) DO ECHO  --- %%S: !@DONTSCAN-%%S: =, !
	 ) ELSE (
		 ECHO  --- !@DONTSCAN: =, !
	 )
	 IF DEFINED @MAX_SIZE ECHO  --- Files ^>%@MAX_SIZE% bytes will be ignored
	 ECHO:
	 ECHO CRITICAL FILES/FOLDERS TO WATCH:
	 ECHO  --- %@WATCHFOR: =, %
	 ECHO %@DIVIDER*%
	 ECHO:
 ) >>"%@SUMMARY%" 2>&1

 FOR %%O IN ("%@LOGFILE%" "%@MONITOR%") DO READABLE "%@SUMMARY%" -O %%O
 %@PAUSE_IF_DEBUG%


 rem ==========================================================================
 rem === Evaluate System Configuration Info
 rem === Updated On: 18 Sep 2025 / 19 Mar 2021 / 28 Nov 2018
 rem ==========================================================================
:EvaluateSystemConfig
 SET @LINES2SKIP="Memory: A;Memory: I;^Available;^Executing: ;^###;^---;%%$;%@VER%"
 FOR %%E IN (NEW OLD %@TEMP_EXTENSIONS% %@FLAG_EXTENSIONS%) DO SET @SYSCONFIG_%%E="%@LOGPATH%\SYSTEM-CONFIG.%%E"
 CALL :ProcessLogs SYSCONFIG

 ( rem -- Write to Log
	 CALL :WriteLn2 "BEGIN CONFIG ANALYSIS..." "" .

	 rem -- Special File Processing
	 CALL :Check4Reset @SYSCONFIG
	 IF EXIST !@SYSCONFIG_NEW! FOR /F "TOKENS=*" %%O IN ('COPY !@SYSCONFIG_NEW! !@SYSCONFIG_OLD! /Y') DO CALL :WriteLn "COPYING OLD REPORT...... %%~O"

	 rem -- Create & CleanUp System Configuration Report
	 CALL :WriteLn "ANALYZE SYSTEM CONFIG..."
	 READABLE "%@SYSINFO%" -A -X --SK=%@LINES2SKIP% -O !@SYSCONFIG_NEW!

	 rem -- Compare Reports, If Previous Report Available
	 IF EXIST !@SYSCONFIG_OLD! (
		 CALL :WriteLn "FINDING CONFIG CHANGES.." 1
		 DIFF -i -y --suppress-common-lines --width 150 !@SYSCONFIG_OLD! !@SYSCONFIG_NEW! >!@SYSCONFIG_DIF! 2>NUL
		 FOR /F "TOKENS=3" %%C IN ('READABLE !@SYSCONFIG_DIF! --TALLY-ONLY') DO SET @CONFIG_CHANGES=%%C

		 ECHO  CHANGED CONFIG ITEMS: !@CONFIG_CHANGES!
		 MAKESTRING ":" 70
		 FOR /F "TOKENS=1-2 DELIMS=*|<>" %%A IN ('READABLE !@SYSCONFIG_DIF!') DO (
			 FOR %%c IN (DEL CHG) DO ECHO  OLD:   %%~A >>!@SYSCONFIG_%%~c!
			 FOR %%c IN (ADD CHG) DO ECHO  NEW:%%~B    >>!@SYSCONFIG_%%~c!
			 ECHO: >>!@SYSCONFIG_CHG!
		 )
		 IF EXIST !@SYSCONFIG_ADD! READABLE !@SYSCONFIG_ADD!
		 ECHO:
		 ECHO:

		 rem -- Log Monitored Changes to Config
		 IF EXIST !@SYSCONFIG_CHG! (
			 SET @CHANGES=Y
			 ECHO:
			 ECHO *** The System Configuration Info Has Been Changed ***
			 ECHO %@DIVIDER-%
			 READABLE !@SYSCONFIG_CHG!
			 ECHO %@DIVIDER-%
			 ECHO:
			 ECHO:
		 ) >>"%@MONITOR%" 2>NUL

	 ) ELSE (
		 SET @CHANGES=*
		 SET @STATUS=*** NEW SYSCONFIG CHECK ***
		 SET @JOBSTATUS= // !@STATUS!
		 FOR /F "TOKENS=3" %%C IN ('READABLE !@SYSCONFIG_NEW! --TALLY-ONLY') DO SET @CONFIG_CHANGES=%%C

		 CALL :WriteLn "FIRST TIME ASSESSMENT..." 2
		 ECHO  FIRST-TIME CONFIG CHECK w/!@CONFIG_CHANGES! Elements
		 MAKESTRING "<" 70
		 READABLE !@SYSCONFIG_NEW!
		 ECHO:
	 )
 ) >>"%@LOGFILE%" 2>&1
 CALL :ProcessLogs SYSCONFIG "%@FLAG_EXTENSIONS%"
 %@PAUSE_IF_DEBUG%


 rem ==========================================================================
 rem === Obtain and Evaluate Current Audit Policy
 rem === Updated On: 18 Sep 2025 / 28 Jul 2024 / 10 Jan 2022 / 23 Jan 2019
 rem ==========================================================================
:EvaluateAuditPolicy
 FOR %%E IN (NEW OLD %@TEMP_EXTENSIONS% %@FLAG_EXTENSIONS%) DO SET @POLICY_%%E="%@LOGPATH%\AUDIT-POLICY.%%E"
 CALL :ProcessLogs POLICY

 ( rem -- Write to Log
	 CALL :WriteLn2 "BEGIN POLICY ANALYSIS..." "" .

	 rem -- Special File Processing
	 CALL :Check4Reset @POLICY
	 IF EXIST !@POLICY_NEW! FOR /F "TOKENS=*" %%O IN ('COPY !@POLICY_NEW! !@POLICY_OLD! /Y') DO CALL :WriteLn "COPYING OLD REPORT...... %%~O"

	 rem -- Create & CleanUp Audit Policy Report
	 CALL :WriteLn "OBTAINING AUDIT REPORT.."
	 (IF !@OSMAJ! GEQ 6 ("%SystemRoot%\System32\AUDITPOL.EXE" /GET /CATEGORY:*) ELSE ("%@SCRIPTS%\Utils\AUDITPOL.EXE" %~1)) >!@POLICY_NEW!

	 ( rem -- Write to Log
		 DATEINFO -S -O "\n\n\v -- System Audit Policy Info\n\*"
		 READABLE !@POLICY_NEW! -X -E --SK "\nSystem Audit Policy\n" --LA "\nCategory" --SB "\nLogon;\nObject;\nPrivilege;\nDetailed;\nPolicy;\nAccount;\nDS" --DL 1 --NL 2
	 ) >>"%@SYSINFO%" 2>&1

	 rem -- Compare Reports, If Previous Report Available
	 IF EXIST !@POLICY_OLD! (
		 CALL :WriteLn "FINDING POLICY CHANGES.." 1
		 DIFF -i -y --suppress-common-lines --width 128 !@POLICY_OLD! !@POLICY_NEW! >!@POLICY_DIF! 2>NUL
		 FOR /F "TOKENS=3" %%C IN ('READABLE !@POLICY_DIF! -F "^|" --TALLY-ONLY') DO SET @POLICY_CHANGES=%%C

		 ECHO  CHANGED POLICY ITEMS: !@POLICY_CHANGES!
		 MAKESTRING ":" 70
		 FOR /F "TOKENS=1-2 DELIMS=*|<>" %%A IN ('READABLE !@POLICY_DIF!') DO (
			 FOR %%c IN (DEL CHG) DO (CALL ECHO  OLD:   %%~A) >>!@POLICY_%%~c!
			 FOR %%c IN (ADD CHG) DO (CALL ECHO  NEW:%%~B) >>!@POLICY_%%~c!
			 ECHO: >>!@POLICY_CHG!
		 )
		 IF EXIST !@POLICY_ADD! READABLE !@POLICY_ADD!
		 ECHO:
		 ECHO:

		 rem -- Log Monitored Changes to Policy
		 IF EXIST !@POLICY_CHG! (
			 SET @CHANGES=Y
			 ECHO:
			 ECHO *** The Windows Audit Policy Has Been Changed ***
			 ECHO %@DIVIDER-%
			 READABLE !@POLICY_CHG!
			 ECHO %@DIVIDER-%
			 ECHO:
			 ECHO:
		 ) >>"%@MONITOR%" 2>NUL

	 ) ELSE (
		 SET @CHANGES=*
		 SET @STATUS=*** NEW POLICY CHECK ***
		 SET @JOBSTATUS=!@JOBSTATUS! // !@STATUS!
		 FOR /F "TOKENS=3" %%C IN ('READABLE !@POLICY_NEW! -F "^|" --TALLY-ONLY') DO SET @POLICY_CHANGES=%%C

		 CALL :WriteLn "FIRST TIME ASSESSMENT..." 2
		 ECHO  FIRST-TIME POLICY AUDIT w/!@POLICY_CHANGES! Elements
		 MAKESTRING "<" 70
		 READABLE !@POLICY_NEW!
		 ECHO:
	 )
 ) >>"%@LOGFILE%" 2>&1
 CALL :ProcessLogs POLICY "%@FLAG_EXTENSIONS%"
 %@PAUSE_IF_DEBUG%


 rem ==========================================================================
 rem === Obtain File Hashes and Assess Each Drive
 rem === Updated On: 18 Sep 2025 / 30 Aug 2024 / 28 Jul 2024 / 19 Mar 2021
 rem ==========================================================================
:AssessDrives
 CALL :WriteLn2 "DRIVE[S] TO ASSESS...... %@DRIVES2SCAN%" >NUL
 CALL :WriteLn "DRIVE[S] TO EXCLUDE..... %@EXCLUDED%"    >NUL

 ( rem -- Write to Log
	 FOR %%D IN (%@DRIVES2SCAN%) DO (
		 CALL :GetTime @SCAN_BEG

		 IF NOT EXIST "%%~D\" (
			 CALL :WriteLn2 "CANNOT ACCESS DRIVE %%~D" 2 .
			 SET @CHANGES=X
			 SET @STATUS-%%~D=*** NO READ ACCESS [%%~D] ***
		 ) ELSE (
			 CALL :WriteLn2 "BEGIN DRIVE ANALYSIS.... %%~D" 1 .
			 FOR %%V IN (SCANNED CHANGED ADDED DELETED) DO SET @%%V=0
			 SET @DRV=%%~D
			 SET @DRV=!@DRV:~0,1!
			 SET @HASH_DB="%@LOGPATH%\!@DRV!-DRIVE.%@TODAY%-%@NOW%.STORE"
			 FOR %%E IN (NEW OLD %@TEMP_EXTENSIONS% %@FLAG_EXTENSIONS%) DO SET @HASH_%%~E="%@LOGPATH%\!@DRV!-DRIVE.%%~E"
			 CALL SET @DONTSCAN=%%@DONTSCAN-!@DRV!:"=%%
			 CALL :ProcessLogs HASH

			 rem -- Get Volume Name
			 FOR /F "TOKENS=5*" %%i IN ('DIR %%~D\ 2^>NUL ^| FIND "Volume in drive"') DO SET @VOL_%%D=%%j
			 SET @VOL=!@VOL:"=!

			 rem -- Enable .ENC (Encryption) Flag if current Hash Database is Different from Current Encryption Mode
			 CALL :WriteLn "PRE-PROCESSING LOGS....."
			 IF EXIST !@HASH_NEW! FOR /F %%p IN ('READABLE !@HASH_NEW! --ST -1 --SK " %@MARKER%" 2^>NUL') DO ECHO %%~p >!@HASH_ENC!

			 rem -- Special File Processing
			 IF EXIST !@HASH_LOG! READABLE !@HASH_LOG! -O !@HASH_NEW!
			 CALL :Check4Reset @HASH
			 IF EXIST !@HASH_NEW! FOR /F "TOKENS=*" %%O IN ('COPY !@HASH_NEW! !@HASH_OLD! /Y') DO CALL :WriteLn "COPYING OLD LOGFILE..... %%~O"

			 rem -- Convert @DONTSCAN List to a File for FILEHASH
			 (FOR %%E IN (// %@DONTSCAN%) DO ECHO %%~E) >"%@TEMPLOG%"

			 rem -- Create/Process Database of File Hashes
			 CALL :WriteLn "CREATING HASH DATABASE.."
			 %@FILEHASH% --%@HASHOPT% -f -m "%@MAX_SIZE%" -o !@HASH_DB! -e "%@TEMPLOG%" -r -d "%%~D" %@INCLUDE% 2>NUL
			 COPY !@HASH_DB! !@HASH_NEW! /Y >NUL

			 rem -- Count the Files Scanned
			 CALL :WriteLn "VERIFYING FILE COUNT...."
			 FOR /F "TOKENS=3" %%C IN ('READABLE !@HASH_NEW! -F "*" --TALLY-ONLY') DO FOR %%I IN (TOTAL_SCAN SCANNED) DO SET /A @%%I+=%%C

			 rem -- Compare Hashes, If Possible
			 IF EXIST !@HASH_OLD! (
				 CALL :WriteLn "SORTING HASH DATABASES.."
				 SORT /+%@SORTLEN% !@HASH_NEW! /O !@HASH_NEW!
				 SORT /+%@SORTLEN% !@HASH_OLD! /O !@HASH_OLD!

				 CALL :WriteLn "COMPARING FILE HASHES..."
				 DIFF -i -y --suppress-common-lines --width 520 !@HASH_OLD! !@HASH_NEW! >!@HASH_DIF! 2>NUL

				 CALL :WriteLn "DISPLAY FINAL REPORTS..." 2
				 FOR /F "TOKENS=3" %%C IN ('READABLE !@HASH_DIF! -F "^|" --TALLY-ONLY') DO FOR %%I IN (TOTAL_CHG CHANGED) DO SET /A @%%I+=%%C
				 FOR /F "TOKENS=3" %%C IN ('READABLE !@HASH_DIF! -F "^>" --TALLY-ONLY') DO FOR %%I IN (TOTAL_ADD ADDED)   DO SET /A @%%I+=%%C
				 FOR /F "TOKENS=3" %%C IN ('READABLE !@HASH_DIF! -F "^<" --TALLY-ONLY') DO FOR %%I IN (TOTAL_DEL DELETED) DO SET /A @%%I+=%%C

				 ECHO  [!TIME: =0!] CHANGED FILES: !@CHANGED!
				 MAKESTRING ":" 70
				 FOR /F "TOKENS=2 DELIMS=*|<>" %%F IN ('READABLE !@HASH_DIF! -F "^|"') DO ECHO  ~ %%~D\%%~F >>!@HASH_CHG!
				 IF EXIST !@HASH_CHG! SORT !@HASH_CHG!
				 ECHO:
				 ECHO:

				 ECHO  [!TIME: =0!] ADDED FILES: !@ADDED!
				 MAKESTRING ">" 70
				 FOR /F "TOKENS=3 DELIMS=*|<>" %%F IN ('READABLE !@HASH_DIF! -F "^>"') DO ECHO  + %%~D\%%~F >>!@HASH_ADD!
				 IF EXIST !@HASH_ADD! SORT !@HASH_ADD!
				 ECHO:
				 ECHO:

				 ECHO  [!TIME: =0!] DELETED FILES: !@DELETED!
				 MAKESTRING "<" 70
				 FOR /F "TOKENS=2 DELIMS=*|<>" %%F IN ('READABLE !@HASH_DIF! -F "^<"') DO ECHO  - %%~D\%%~F >>!@HASH_DEL!
				 IF EXIST !@HASH_DEL! SORT !@HASH_DEL!
				 ECHO:

				 rem -- Log Monitored Changes to File
				 FOR %%L IN (CHG ADD DEL) DO IF EXIST !@HASH_%%L! (
					 IF DEFINED @WATCHFOR FINDSTR /I /L "%@WATCHFOR:"=%" !@HASH_%%L! >"%@TEMPLOG%"
					 FOR /F "TOKENS=3" %%c IN ('READABLE "%@TEMPLOG%" -F "\" --TALLY-ONLY') DO IF NOT "%%~c"=="0" (
						 SET /A @WATCHCOUNT+=%%c
						 ECHO:
						 ECHO [!TIME: =0!] *** %%L *** ON DRIVE %%D = %%c
						 ECHO %@DIVIDER-%
						 READABLE "%@TEMPLOG%"
						 ECHO:
						 ECHO:
					 ) >>"%@MONITOR%" 2>NUL
				 )

			 ) ELSE (
				 SET @CHANGES=*
				 SET @STATUS-%%~D=*** NEW DRIVE [%%~D] ***
				 IF EXIST !@HASH_ZZZ! SET @STATUS-%%~D=*** HASH DB RESET [%%~D] ***
				 IF EXIST !@HASH_ENC! SET @STATUS-%%~D=*** HASH ALGORITHM CHANGE [%%~D] ***
				 FOR %%I IN (TOTAL_ADD ADDED) DO SET /A @%%I+=@SCANNED

				 CALL :WriteLn "INITIAL [RE]ASSESSMENT.." 2
				 ECHO  [!TIME: =0!] ADDED FILES: !@ADDED!
				 MAKESTRING "<" 70
				 FOR /F "TOKENS=2 DELIMS=*" %%F IN ('READABLE "!@HASH_NEW!" --SK "%@NOACCESS%"') DO ECHO %%~D\%%~F
				 ECHO:
			 )

			 rem -- Display Summary Info for Current Drive & Process Logs
			 IF DEFINED @STATUS-%%~D SET @JOBSTATUS=!@JOBSTATUS! // !@STATUS-%%~D!
			 CALL :CurrentTally "%%~D" !@SCANNED! !@CHANGED! !@ADDED! !@DELETED! "%%~D"
			 CALL :ProcessLogs HASH "%@FLAG_EXTENSIONS%"
		 )
	 )
 ) >>"%@LOGFILE%" 2>&1
 %@PAUSE_IF_DEBUG%


 rem ==========================================================================
 rem === Display Summary Counts for Policy/Config Changes & Watched Files
 rem === Updated On: 01 Dec 2018 / 28 Nov 2018 / 03 Jun 2016
 rem ==========================================================================
:GetSummaryInfo
 CALL :WriteLn2 "UPDATING SUMMARY INFO..." >NUL
 IF NOT "!@TOTAL_CHG!/!@TOTAL_ADD!/!@TOTAL_DEL!/!@CONFIG_CHANGES!/!@POLICY_CHANGES!"=="0/0/0/0/0" SET @CHANGES=Y
 CALL :GetSummaryCounts 2 >>"%@MONITOR%" 2>&1
 ECHO:
 %@PAUSE_IF_DEBUG%


 rem ==========================================================================
 rem === Keep Only The Last %@KEEP_HISTORICAL% Integrity Checks (Default=7)
 rem === Updated On: 26 Sep 2025 / 18 Aug 2025 / 12 May 2016 / 09 Mar 2015
 rem ==========================================================================
:PruneOldAudits
 CALL :WriteLn2 "PRUNING OLD ASSESSMENTS.." >NUL

 ( rem -- Write to Log
	 CALL :WriteLnLog "Pruning Audit Databases [Keep latest %@KEEP_HISTORICAL%]" 0 .
	 FOR /F "TOKENS=1 DELIMS=-" %%D IN ('DIR /B "%@LOGPATH%\*.STORE"') DO @SET @DRV-%%~D=%%~D
	 FOR /F "TOKENS=2 DELIMS==" %%D IN ('SET "@DRV-" 2^>NUL') DO (
		 ECHO:
		 ECHO Evaluating Audits on Drive %%D: ...
		 FOR /F "SKIP=%@KEEP_HISTORICAL% TOKENS=*" %%F IN ('DIR "%@LOGPATH%\%%~D-DRIVE*.STORE" /A-D /O-N /B') DO (
			 ECHO  - Deleting "%%F" from Integrity Validation Folder
			 DEL "%@LOGPATH%\%%~F" >NUL
		 )
	 )

	 SET @FORFILES=FILEHASH --JF --OLDER %@OBSOLETE% -D "%@LOGPATH%" ?-DRIVE.*
	 ECHO:
	 CALL :WriteLnLog "Removing Obsolete Databases [Greater than %@OBSOLETE% Days Old]" 0 .
	 FOR /F "TOKENS=*" %%O IN ('!@FORFILES! 2^>NUL') DO (
		 ECHO  - Deleting Obsolete Data: "%%~O"
		 DEL "%%~O" >NUL
	 )
	 ECHO:
 ) >>"%@LOGFILE%" 2>&1

 rem -- Delete Obsolete Files
 FOR %%Z IN (DB XML INFO) DO IF EXIST "%@LOGPATH%\*.%%~Z" DEL "%@LOGPATH%\*.%%~Z" >NUL
 %@PAUSE_IF_DEBUG%


 rem ==========================================================================
 rem === Compress Log Files and Send via Email To Appropriate Group(s)
 rem === Updated On: 18 Sep 2025 / 01 Sep 2025 / 31 Aug 2025 / 10 Dec 2019 / 25 Jan 2018
 rem ==========================================================================
:SendLog
 CALL :SaveJobHistory

 CALL :WriteLn "UPDATING JOB STATISTICS.." >NUL
 CALL :CurrentTally "%@DRIVES2SCAN%" !@TOTAL_SCAN! !@TOTAL_CHG! !@TOTAL_ADD! !@TOTAL_DEL! "TOTAL" >>"%@LOGFILE%" 2>&1

 CALL :WriteLn "CREATING ZIP ARCHIVE....." >NUL
 SET @FILELIST=& FOR %%F IN ("%@SYSINFO%" "%@MONITOR%" "%@TIMINGS%" "%@LOGFILE%") DO IF EXIST %%F SET @FILELIST=!@FILELIST! %%F

 rem -- Determine If Email Status Should Be Sent
 IF /I "%@MAIL%"=="TRUE" IF /I NOT "%@CHANGES%"=="N" SET @MAILIT=TRUE
 IF /I "%@MAIL%"=="WATCH" IF /I NOT "%@WATCHCOUNT%"=="0" SET @MAILIT=TRUE

 ( rem -- Write to Log
	 CALL :WriteLnLog "Compressing Log Files for Email Purposes" 1 .
	 PUSHD "%@LOGPATH%"
	 ZIP -9 -v -T -j "%@ARCHIVE%" !@FILELIST!
	 POPD
	 ECHO:
	 ECHO Job Logs Compressed -- [!DATE! at !TIME!]
	 ECHO:
	 FILEHASH --%@LOG_HASH% -g "%@CHECKSUM%" -d "%@ZIPPATH%" "%COMPUTERNAME%-%~n0*"
	 ECHO:
	 READABLE "%@CHECKSUM%" -A
	 ECHO:
 ) >>"%@LOGFILE%" 2>&1

 IF /I "!@MAILIT!"=="TRUE" IF DEFINED @RCPT-TO (
	 CALL :WriteLn "MAILING REPORT ARCHIVE... %@RCPT-TO:-to =%" >NUL
	 ( rem -- Write to Log
		 CALL :WriteLnLog "Emailing Report Archive" 1 .
		 ECHO Sending Logs to: %@RCPT-TO:-to =%
		 ECHO              on: !DATE! at !TIME!
		 ECHO:
		 BLAT -install %@MAILSERVER% %@MAILSENDER% 2 %@MAILPORT%
		 ECHO:
		 FOR %%F IN (!@FILELIST! "%@CHECKSUM%" "%@ARCHIVE%") DO DIR "%%~F" | FIND /I "%%~nxF"
		 ECHO:
		 READABLE "%@CHECKSUM%" -A -F "%@ZIPSTAMP%"
		 ECHO %@DIVIDER-%
		 ECHO:
	 ) >"%@TEMPLOG%" 2>&1
	 FOR %%O IN (CON "%@SUMMARY%" "%@LOGFILE%") DO READABLE "%@TEMPLOG%" -O %%O

	 ECHO:
	 BLAT "%@SUMMARY%" -f %@MAILSENDER% %@RCPT-TO% -s "%~n0 Job on %COMPUTERNAME%" -attach "%@ARCHIVE%","%@CHECKSUM%"
	 CALL :ForwardToSyslog "6" Mailed Complete %~n0 Job Report -- "%@ARCHIVE%" >NUL
 )
 %@PAUSE_IF_DEBUG%


 rem ==========================================================================
 rem === Perform Log Management/Maintenance (Or Copy Them for Debug Purposes)
 rem === Updated On: 18 Sep 2025 / 12 Jan 2015 / 24 Dec 2014
 rem ==========================================================================
:LogMaint
 CALL :WriteLn2 "PERFORMING LOG CLEANUP..." >NUL

 COPY "%@LOGFILE%" + "%@MONITOR%" "%@LOGFILE%" /Y

 IF /I "%DEBUG%"=="COPY" FOR %%L IN (%@LOGLIST% %@CLEANUP%) DO IF EXIST %%L COPY %%L "%TEMP%" /Y

 FOR %%L IN (%@LOGLIST%) DO IF EXIST %%L (
	 SET @THISLOG=%%L
	 COPY %%L !@THISLOG:%@FILESTAMP%=TXT! /Y
	 IF %%L=="%@LOGFILE%" (ECHO: & READABLE %%L & DIR %%L)
 )
 FOR %%L IN (%@CLEANUP%) DO IF EXIST %%L DEL %%L >NUL
 %@PAUSE_IF_DEBUG%


 rem ==========================================================================
 rem === Reset Environment Variables and Exit Batch File
 rem === Updated On: 03 Jul 2024 / 03 Oct 2019 / 28 Feb 2019
 rem ==========================================================================
:ExitBatch
 CALL :ShowStatus "FINISHED"
 ENDLOCAL
 EXIT /B


 rem ==========================================================================
 rem === SUBROUTINE: Log Output to Console and/or Logfile
 rem === Updated On: 13 May 2016 / 15 Jan 2015
 rem ==========================================================================
:WriteLn
:WriteLn2
:WriteLnLog
 rem %1 = Message to Log/Echo
 rem %2 = Optional (Number of Blank Lines After Header)
 rem %3 = If Non-Blank, then Display Headers
 rem %4 = Optional (Select Character for Divider)

 SET @TIME=!TIME: =0!
 SET @OUT=%~1& IF "%~1"=="" (SET @OUT= )
 SET @OUT=!@OUT:(={!
 SET @OUT=!@OUT:)=}!
 SET @OUT_F=[!DATE! at !@TIME!] -- !@OUT!
 SET @OUT_M=[!DATE:~0,3! at !@TIME:~0,5!] !@OUT!

 rem -- Prepend a Blank Line
 IF /I "%~0"==":WriteLn2" ECHO: >CON

 IF "%~3"=="" (
	 ECHO  !@OUT_M!
 ) ELSE (
	 ECHO:
	 ECHO !@DIVIDER%~4!
	 ECHO  !@OUT_F!
	 ECHO !@DIVIDER%~4!
 )

 rem -- Option to Skip Console Output
 IF /I NOT "%~0"==":WriteLnLog" ( ECHO  !@OUT_M! ) >CON
 FOR /L %%Z IN (1,1,%~2) DO ECHO:
 %@PRINT_IF_DEBUG%
 EXIT /B


 rem ==========================================================================
 rem === SUBROUTINE: Obtain One System Configuration Item (WMIC or Regular)
 rem === Updated On: 27 Nov 2018
 rem ==========================================================================
:GetWMIC
:GetConfig
:GetConfig2
 rem %* = Command + Parameters to Execute

 SET @CMD=
 SET @MSG=
 IF /I "%~0"==":GetWMIC" (
	 SET @CMD= WMIC /OUTPUT:"%@TEMPLOG%"
	 SET @MSG= WMIC
 )

 ECHO %@DIVIDER-%
 ECHO Executing:%@MSG% %*
 ECHO %@DIVIDER-%

:ShowHeader
 %@CMD% %*
 IF /I NOT "%~0"==":GetConfig" IF EXIST "%@TEMPLOG%" TYPE "%@TEMPLOG%"
 ECHO %@DIVIDER#%
 ECHO:
 ECHO:
 %@PAUSE_IF_DEBUG%
 EXIT /B


 rem ==========================================================================
 rem === SUBROUTINE: Check for Special Flags and Reset Logs if Necessary
 rem === Updated On: 28 Nov 2018
 rem ==========================================================================
:Check4Reset
 rem %1 = Target Variable Prefix

 SET @NEWLOGS=
 FOR %%f IN (%@FLAG_EXTENSIONS%) DO IF EXIST !%~1_%%~f! SET @NEWLOGS=TRUE
 IF DEFINED @NEWLOGS FOR %%g IN (NEW OLD) DO IF EXIST !%~1_%%~g! DEL !%~1_%%~g! >NUL
 %@PRINT_IF_DEBUG%
 EXIT /B


 rem ==========================================================================
 rem === SUBROUTINE: Get Count of Total, Added, and Changed Files
 rem === Updated On: 18 Sep 2025 / 14 Jul 2021 / 05 May 2020
 rem ==========================================================================
:CurrentTally
 rem %1 = Target Drive(s)
 rem %2 = Value to Display for Files Scanned
 rem %3 = Value to Display for Files Changed
 rem %4 = Value to Display for Files Added
 rem %5 = Value to Display for Files Deleted
 rem %6 = Additional Text To Display

 rem -- For Scans involving a Single Drive, Display Final Stats Only
 IF %@D2S_COUNT% LEQ 1 IF /I NOT "%~6"=="TOTAL" EXIT /B
 IF DEFINED @STATUS-%~1 FOR %%d IN (%~1) DO (
	 SET @OVERVIEW=!@STATUS-%%~d!
	 SET @OVERVIEW=!@OVERVIEW: [%%~d]=!
 ) ELSE (
	 SET @OVERVIEW=
 )

 CALL :PadLeft @TOT "%~2" "." 16
 CALL :PadLeft @CHG "%~3" "." 16
 CALL :PadLeft @ADD "%~4" "." 16
 CALL :PadLeft @DEL "%~5" "." 16

( rem -- Write to Log
	 CALL :WriteLn "SCANNING SUMMARY for.... %~1 !@OVERVIEW!" 0 . -
	 ECHO  %~6 Files Scanned ..%@TOT% ^(Executables ^& Scripts^)
	 ECHO  %~6 Files Changed ..%@CHG%
	 ECHO  %~6 Files Added ....%@ADD%
	 ECHO  %~6 Files Deleted ..%@DEL%
	 ECHO %@DIVIDER-%

	 rem -- Show Disk Stats
	 FOR %%d IN (%~1) DO FOR /F "TOKENS=*" %%v IN ('PSINFO -d 2^>NUL ^| FIND /I " %%d: Fixed "') DO (
		 CALL :CleanDriveInfo "%%~v" "!@VOL_%%d!"
	 )
	 ECHO %@DIVIDER-%

	 IF /I "%~6"=="TOTAL" (
		 CALL :GetTime @SCRIPT_END
		 ECHO %@DIVIDER#%
		 ECHO  File Integrity Verification Started On ........ %@SCRIPT_BEG%
		 ECHO  File Integrity Verification Ended On .......... !@SCRIPT_END!
		 DATEINFO -t !@SCRIPT_BEG#! -n !@SCRIPT_END#! -e "dys hrs mins secs.ms" -o " File Integrity Verification Duration .......... \v\n\#" 2>NUL
		 CALL :GetSummaryCounts 1
		 ECHO  ASSESSMENT ON %@LOCALHOST% COMPLETED [%@VER%]
		 ECHO %@DIVIDER#%
	 ) ELSE (
		 CALL :GetTime @SCAN_END
		 ECHO %@DIVIDER*%
		 ECHO  Drive Analysis Started On ..................... %@SCAN_BEG%
		 ECHO  Drive Analysis Ended On ....................... !@SCAN_END!
		 DATEINFO -t !@SCAN_BEG#! -n !@SCAN_END#! -e "dys hrs mins secs.ms" -o " Drive Analysis Duration ....................... \v\n\*" 2>NUL
	 )
	 ECHO:
	 ECHO:
 ) >"%@TEMPLOG%" 2>&1

 rem -- Display Log and Send Output to Other Logs as Necessary
 READABLE "%@TEMPLOG%"
 IF /I "%~6"=="TOTAL" (
	 READABLE "%@TEMPLOG%" -O "%@REPORT%"
	 READABLE "%@TEMPLOG%" >>"%@SUMMARY%" 2>&1
 )
 %@PRINT_IF_DEBUG%
 EXIT /B


 rem ==========================================================================
 rem === SUBROUTINE: Display Summary Counts for Policy Changes & Watched Files
 rem === Updated On: 28 Nov 2018 / 13 May 2016
 rem ==========================================================================
:GetSummaryCounts
 rem %1 = Optional Number of Blank Lines After Summary

 CALL :PadLeft @WC "!@WATCHCOUNT!" "." 11
 CALL :PadLeft @CC "!@CONFIG_CHANGES!" "." 11
 CALL :PadLeft @PC "!@POLICY_CHANGES!" "." 12

 ECHO  # OF WATCHED FILES CHANGED.!@WC!
 ECHO  # OF SYSTEM CONFIG CHANGES.!@CC!
 ECHO  # OF AUDIT POLICY CHANGES.!@PC!
 FOR /L %%Z IN (1,1,%~1) DO ECHO:
 %@PAUSE_IF_DEBUG%
 EXIT /B


 rem ==========================================================================
 rem === SUBROUTINE: Store Current Date and Time in Variable
 rem === Updated On: 16 Oct 2023 / 23 Mar 2015
 rem ==========================================================================
:GetTime
 rem %1 = Variable to Store Date/Time

 SET %~1=!DATE! at !TIME: =0!
 FOR /F "TOKENS=*" %%d IN ('DATEINFO -S %@DATEFMT% -Q 2^>NUL') DO SET %~1#="%%~d"
 EXIT /B


 rem ==========================================================================
 rem === SUBROUTINE: Display Drive Summary Info
 rem === Updated On: 21 Nov 2014 / 19 Nov 2014
 rem ==========================================================================
:CleanDriveInfo
 rem %1 = Current Drive Stats
 rem %2 = Volume Label

 SET @TMP=%~1
 IF NOT "%~2"=="" SET @TMP=!@TMP:%~2=!
 FOR /F "TOKENS=1-7 DELIMS= " %%a IN ('ECHO !@TMP!') DO (
	 CALL :PadRight @DL "%%a" " " 2
	 CALL :PadRight @DT "%%c" " " 5
	 CALL :PadLeft  @DS "%%f" "." 13
	 CALL :PadLeft  @DF "%%e" "." 13
	 CALL :PadLeft  @DU "%%g" "." 4
	 ECHO  !@DL! !@DT!  Total.!@DS!  Free.!@DF!  Used.!@DU!%% - %~2
 )
 %@PRINT_IF_DEBUG%
 EXIT /B


 rem ==========================================================================
 rem === SUBROUTINE: Change Variable Contents to Uppercase
 rem === Updated On: 03 Mar 2017
 rem ==========================================================================
:UpperCase
 rem %1 = Variable to Impact

 IF NOT "%~1"=="" FOR /F "TOKENS=*" %%v IN ('CHANGECASE -u "!%~1!"') DO SET %~1=%%v
 %@PRINT_IF_DEBUG%
 EXIT /B


 rem ==========================================================================
 rem === Ensure Unique Parameters in a Command String
 rem === Updated On: 13 Mar 2017 / 11 Mar 2017
 rem ==========================================================================
:EnsureUnique
 rem %1 = Name of Variable to Process

 IF "%~1"=="" EXIT /B

 SET @UNIQUE=
 FOR %%P IN (!%~1!) DO IF NOT DEFINED $%%P (
	 SET $%%P=YES
	 SET @UNIQUE=!@UNIQUE! %%P
 )
 SET @UNIQUE=!@UNIQUE: =!
 SET @UNIQUE=!@UNIQUE::=: !
 SET %~1=!@UNIQUE!

 rem -- CleanUp Temp Variables
 FOR %%P IN (!%~1!) DO SET $%%P=
 %@PRINT_IF_DEBUG%
 EXIT /B


 rem ==========================================================================
 rem === SUBROUTINE: Cleanup Temp Logs (Or Copy Them for Debug Purposes)
 rem === Updated On: 03 Mar 2017 / 03 Jun 2016
 rem ==========================================================================
:ProcessLogs
 rem %1 = Prefix of Logs to Process
 rem %2 = Additional Extensions

 IF /I "%DEBUG%"=="COPY" FOR %%E IN (%~2 %@TEMP_EXTENSIONS% NEW OLD DB) DO IF EXIST "!@%~1_%%~E!" COPY "!@%~1_%%~E!" "%TEMP%" /Y >NUL
                         FOR %%E IN (%~2 %@TEMP_EXTENSIONS%) DO IF EXIST "!@%~1_%%~E!" DEL "!@%~1_%%~E!" >NUL
 %@PRINT_IF_DEBUG%
 EXIT /B


 rem ==========================================================================
 rem === SUBROUTINE: Validate Hash and Obtain Associated Settings
 rem === Updated On: 06 May 2020
 rem ==========================================================================
:ValidateHash
 rem %1 = Variable of Hash to Verify
 rem %2 = Variable to store Sort Length
 rem %3 = Variable to store Marker (Optional)

 SET @VALID=
 FOR %%H IN (%@HASH_MATRIX%) DO (
	 FOR /F "TOKENS=1-3* DELIMS=:" %%L IN ('ECHO %%~H') DO (
		 FOR %%A IN (%%~L %%~N) DO (
			 IF /I "!%~1!"=="%%~A" (
				 SET @VALID=TRUE
				 SET /A %~2=%%~M + 2
				 IF "%%~N"=="" (
					SET %~1=%%~A
					IF NOT "%~3"=="" SET %~3=?%%~A*
				 ) ELSE (
					SET %~1=%%~N
					IF NOT "%~3"=="" SET %~3=?%%~N*
				 )
				 IF "%%~O"=="+" (SET %~3=*)
			 )
		 )
	 )
 )

 rem -- If No Match Is Found, Retry with Default Hash
 IF NOT DEFINED @VALID (
	 SET @VALID=DEFAULT
	 SET %~1=%@FALLBACK_HASH%
	 CALL :ValidateHash %~1 %~2 %~3
 )
 %@PRINT_IF_DEBUG%
 EXIT /B


 rem ==========================================================================
 rem === SUBROUTINE: Swap Characters in Variable's Content
 rem === Updated On: 26 May 2020
 rem ==========================================================================
:SwapCharacters
 rem %1 = Variable to Make the Swap
 rem %2 = Original Character(s)
 rem %3 = New Character(s)

 IF "%~1"=="" EXIT /B
 SET @VAR=!%~1!
 SET @VAR=!@VAR:"=!
 FOR /F "TOKENS=*" %%a IN ('SUBSTRING "!@VAR!" "%~2" "%~3" 2^>NUL') DO SET @VAR=%%a
 SET @VAR=!@VAR:="!
 SET %~1=!@VAR!
 %@PRINT_IF_DEBUG%
 EXIT /B


 rem ==========================================================================
 rem === SUBROUTINE: Determine if Parameter is a Valid Integer, Or Set to Zero
 rem === Updated On: 21 Jan 2020
 rem ==========================================================================
:GetInteger
 rem %1 = Variable to Store Integer
 rem %2 = Parameter to Evaluate
 rem %3 = Value to Default to, If Zero
 rem %4 = Optional: Lowest Value
 rem %5 = Optional: Highest Value

 SET @V1=%2& IF DEFINED @V1 SET @V1=!@V1:"=!
 SET @V2=%3& IF DEFINED @V2 SET @V2=!@V2:"=!
 SET @V3=%4& IF DEFINED @V3 SET @V3=!@V3:"=!
 SET @V4=%5& IF DEFINED @V4 SET @V4=!@V4:"=!
 FOR /F "TOKENS=3" %%i IN ('CHECKPARAMS -o --min "!@V3!" --max "!@V4!" -n "!@V1!" "!@V2!" 2^>NUL') DO SET %~1=%%i
 %@PRINT_IF_DEBUG%
 EXIT /B


 rem ==========================================================================
 rem === SUBROUTINE: Use CheckParams.exe to Capture Selected Parameters
 rem === Updated On: 21 Jan 2020
 rem ==========================================================================
:GetParams
 rem %1 = Parameters to Search For
 rem %2 = Variable to Set

 SET @OK=& IF "%~1"=="" EXIT /B
 FOR /F "TOKENS=2-3*" %%v IN ('CHECKPARAMS -q -a -c "%~1" -s %@ALL_PARAMS% 2^>NUL') DO IF /I "%%~v"=="TRUE" (
	 SET @OK=T
	 SET @REQ=T
	 IF NOT "%~2"=="" SET %~2=%%~x
 )
 %@PRINT_IF_DEBUG%
 EXIT /B


 rem ==========================================================================
 rem === SUBROUTINE: Pad Variable Content to the Left/Right for Display in Logs
 rem === Updated On: 22 Jan 2020 / 14 Oct 2019
 rem ==========================================================================
:PadLeft
:PadRight
 rem %1 = Variable to Store New Content
 rem %2 = Existing Value to Pad
 rem %3 = Character to use as padding [typically " ", "." or "0"]
 rem %4 = New String Length

 SET @STR=%~2
 SET @LEN=& FOR /L %%c IN (0,1,199) DO IF "!@STR:~%%c,1!"=="" IF NOT DEFINED @LEN SET @LEN=%%c
 IF 1%@LEN% GEQ 1%~4 (
	 SET %~1=%~2
 ) ELSE (
	 IF /I "%~0"==":PadLeft" (
		 SET @TEMPVAR=& FOR /L %%c IN (1,1,%~4) DO SET @TEMPVAR=!@TEMPVAR!%~3
		 SET @TEMPVAR=!@TEMPVAR!%~2
		 SET %~1=!@TEMPVAR:~-%~4!
	 )
	 IF /I "%~0"==":PadRight" (
		 SET @TEMPVAR=%~2& FOR /L %%c IN (1,1,%~4) DO SET @TEMPVAR=!@TEMPVAR!%~3
		 SET %~1=!@TEMPVAR:~0,%~4!
	 )
 )
 %@PAUSE_IF_DEBUG%
 EXIT /B


 rem ==========================================================================
 rem === SUBROUTINE: Log Early Job Execution Failures
 rem === Updated On: 26 Sep 2024
 rem ==========================================================================
:LogCriticalFailure
 rem %* = Optional Message to include in Job History

 SET @TEMPLOG="%TEMP%\%~n0.Errors.TXT"
 CALL :ForwardToSyslog "6" %~n0 // ENDED: !DATE! at !TIME: =0! // %* >>%@TEMPLOG%
 ECHO:
 ECHO For more details, see: %@TEMPLOG%
 %@PRINT_IF_DEBUG%
 EXIT /B


 rem ==========================================================================
 rem === SUBROUTINE: Update Job Execution Stats
 rem === Updated On: 29 Sep 2025 / 18 Sep 2025 / 15 Oct 2019 / 07 Oct 2019
 rem ==========================================================================
:SaveJobHistory
 rem %* = Optional Message to include in Job History

 CALL :WriteLn "UPDATING JOB HISTORY....." >NUL

 CALL :GetTime @SCRIPT_END
 CALL :PadLeft  @DS "!@D2S_COUNT!" "." 2
 CALL :PadLeft  @TS "!@TOTAL_SCAN!" "." 6
 CALL :PadLeft  @TC "!@TOTAL_CHG!" "." 6
 CALL :PadLeft  @TA "!@TOTAL_ADD!" "." 6
 CALL :PadLeft  @TD "!@TOTAL_DEL!" "." 6
 CALL :PadLeft  @WC "!@WATCHCOUNT!" "." 6
 CALL :PadLeft  @CC "!@CONFIG_CHANGES!" "." 4
 CALL :PadLeft  @PC "!@POLICY_CHANGES!" "." 2
 CALL :PadRight @IN "!@DRV_LETTERS!" "-" 20
 CALL :PadRight @EX "!@DRV_EXC!" "-" 20
 SET @ROWCOUNT=0
 SET @TRUNCATED=
 SET @ELAPSED=%@NO_TIME%& FOR /F "DELIMS=" %%d IN ('DATEINFO -t !@SCRIPT_BEG#! -n !@SCRIPT_END#! -e "hr:min:sec.ms" -q 2^>NUL') DO SET @ELAPSED=%%d
 SET @SPECIAL=%@JOBTYPE%& IF NOT "%~1"=="" SET @SPECIAL= // %*
 SET @JOBHISTORY=%@JOBNAME% // DRIVES.%@DS% // FILES.%@TS% // CHANGES:%@CHANGES% [CHG.%@TC% ADD.%@TA% DEL.%@TD% MON.%@WC%  CONFIG.%@CC% POLICY.%@PC%] // DURATION: %@ELAPSED% // START: %@SCRIPT_BEG% // END: !@SCRIPT_END! // SCAN: %@IN: =-% // SKIP: %@EX: =-% // %@HASHOPT% // %~n0 %@VERSION%%@x64_MODE%%@SPECIAL%

 %@VERBOSE_OFF%
 TYPE NUL >>"%@TIMINGS%"
 FOR /F "TOKENS=3" %%c IN ('READABLE "%@TIMINGS%" -F "/" --TALLY-ONLY') DO SET @ROWCOUNT=%%c
 IF !@ROWCOUNT! GTR %@MAXROWS% (
	 SET @TRUNCATED= // TRUNCATED
	 SET @OLDTIME=!@TIMINGS:TXT=%@FILESTAMP%!
	 COPY "%@TIMINGS%" "!@OLDTIME!" /Y >CON
	 READABLE "!@OLDTIME!" --ST -%@MINROWS% -O "%@TIMINGS%" 2>&1
	 ECHO:
	 CALL :ForwardToSyslog "5" %@JOBNAME%: Job History Logfile Truncated -- [!DATE! at !TIME!]
 ) >>"%@LOGFILE%" 2>>"%@ERRORS%"

 CALL :ForwardToSyslog "6" %@JOBHISTORY%!@POLICYSTATUS!!@JOBSTATUS!!@TRUNCATED! >>"%@TIMINGS%" 2>>"%@ERRORS%"
 %@VERBOSE_ON%
 %@PAUSE_IF_DEBUG%
 EXIT /B


 rem ==========================================================================
 rem === SUBROUTINE: Forward Output to Available Syslog Server(s) as LOCAL3
 rem === Updated On: 25 Sep 2024 / 13 Jan 2020 / 07 Mar 2019
 rem ==========================================================================
:ForwardToSyslog
 rem %1 = Syslog Severity Level (RFC 3164)
 rem %* = Message to Send to Display and Send to Syslog Server

 SET @S_MSG=%*& IF "%~2"=="" SET @S_MSG=%1 - This is a Syslog Test Message from %@VER%
 SET @SEV=6
 FOR %%a IN (0 EMERGENCY) DO   IF /I "%~1"=="%%~a" SET @SEV=0
 FOR %%a IN (1 ALERT) DO       IF /I "%~1"=="%%~a" SET @SEV=1
 FOR %%a IN (2 CRITICAL) DO    IF /I "%~1"=="%%~a" SET @SEV=2
 FOR %%a IN (3 ERROR) DO       IF /I "%~1"=="%%~a" SET @SEV=3
 FOR %%a IN (4 WARNING) DO     IF /I "%~1"=="%%~a" SET @SEV=4
 FOR %%a IN (5 NOTICE) DO      IF /I "%~1"=="%%~a" SET @SEV=5
 FOR %%a IN (6 INFORMATION) DO IF /I "%~1"=="%%~a" SET @SEV=6
 FOR %%a IN (7 DEBUG) DO       IF /I "%~1"=="%%~a" SET @SEV=7

 %@VERBOSE_OFF%
 CALL :Prepare4Syslog %@S_MSG%

 ECHO !@S_MSG!
 IF DEFINED @SYSLOG_UTIL (
	 IF /I "%@USE_SYSLOGS%"=="TRUE" (
		 FOR %%s IN ("" 0 1 2 3 4) DO IF DEFINED @SYSLOG%%~s (
			 SET @S_PORT=514& IF DEFINED @SYSLOG%%s_PORT SET @S_PORT=!@SYSLOG%%~s_PORT!
			 "%@SYSLOGGEN%" -t:!@SYSLOG%%~s! -p:!@S_PORT! -f:19 -s:!@SEV! -tg:"%~n0" -m:"!@S_MSG2!" -q -hn:%@LOCALHOST% | FIND /V "%@NO_SYSLOGGEN%"
		 )
	 )

	 IF /I "%@SCRIPT_METRICS%"=="TRUE" (
		 FOR %%P IN (%@MY_SYSLOG_PORTS%) DO (
			 "%@SYSLOGGEN%" -t:127.0.0.1 -p:%%~P -f:19 -s:6 -tg:"%~n0+" -m:"!@S_MSG2!" -q -hn:localhost 2>NUL | FIND /V "%@NO_SYSLOGGEN%"
		 )
	 )
 )
 %@VERBOSE_ON%
 %@PRINT_IF_DEBUG%
 EXIT /B


 rem ==========================================================================
 rem === SUBROUTINE: Prepare Messages for Syslog Utils (NeoLog and SyslogGen)
 rem === Updated On: 23 Jun 2024 / 19 Jan 2020 / 02 Apr 2018
 rem ==========================================================================
:Prepare4Syslog
 rem %* = Syslog Message to be Cleaned Up

 SET @PREFIX=%1
 SET @S_MSG=%*
 SET @S_MSG=!@S_MSG:%@PREFIX% =!

:TrimLeadingSpaces
 IF "!@S_MSG:~0,1!"==" " (
	 SET @S_MSG=!@S_MSG:~1!
	 GOTO :TrimLeadingSpaces
 )

 SET @S_MSG2=!@S_MSG:\=\x5B!
 SET @S_MSG2=!@S_MSG2:"=\x22!
 %@PRINT_IF_DEBUG%
 EXIT /B


 rem ==========================================================================
 rem === SUBROUTINE: Check if Scheduled Job is Already Running
 rem === Updated On: 03 Sep 2024 / 15 May 2018 / 05 Feb 2015
 rem ==========================================================================
:CheckSched
 rem %1 = Standard Search Criteria
 rem %2 = Advanced Search Criteria

 IF "%~3"=="$$" (SET @R=) ELSE (SET @R=/R)

 SET @CHK=%2
 IF DEFINED @CHK (
	 %@CHK_SCHED% 2>NUL | FIND /I "%~1" | FINDSTR /I %@R% /C:%2 >NUL
 ) ELSE (
	 %@CHK_SCHED% 2>NUL | FIND /I "%~1" >NUL
 )

 IF NOT ERRORLEVEL 1 EXIT /B
	 %@SET_SCHED%
	 IF NOT DEFINED @JOBTYPE SET @JOBTYPE= // SCHEDULED JOB CHANGED

 %@PAUSE_IF_DEBUG%
 EXIT /B


 rem ==========================================================================
 rem === SUBROUTINE: Set/Display Script Version and Execution Status
 rem === Updated On: 22 Sep 2025 / 16 Oct 2023 / 24 Dec 2019
 rem ==========================================================================
:ShowStatus
 rem %1 = Run Status of Script
 rem %2 = Current Application Version

 IF NOT DEFINED @DATEFMT SET @DATEFMT=-F "mm/dd/yyyy hh:nn:ss.zzz"
 SET @SCRIPTSTATUS=%~1& IF "%~1"=="" SET @SCRIPTSTATUS=RUNNING
 IF NOT "%~2"=="" (SET @VER=%~nx0 %~2&     SET @VERSION=%~2)
 IF NOT "%~3"=="" (SET @VER=%~nx0 %~2 %~3& SET @VERSION=%~2 %~3)
 IF /I "%~1"=="STARTED" CALL :GetTime @SCRIPT_BEG
 IF /I "%~1"=="FINISHED" (
	 IF DEFINED $CODEPAGE FOR /F "TOKENS=1* DELIMS=:" %%B IN ('CHCP %$CODEPAGE%') DO SET @CHCP_STATUS= {Restoring Code Page:%%C}
	 IF DEFINED @END_DEBUG_MODE %@END_DEBUG_MODE:"=%
	 TITLE %@CUSTOM_TITLE% [%USERDOMAIN%\%USERNAME%]   !@DEBUG_MODE!
	 DATEINFO -t %@SCRIPT_BEG#% -e "hr:min:sec.ms" -o "\n*** DURATION: " 2>NUL
 )
 NOW \n*** %@SCRIPTSTATUS%: %@VER% [\v] *** %@CHCP_STATUS%\n!@CRLF-%~1!
 EXIT /B


 rem ==========================================================================
 rem === SUBROUTINE: Display Syntax Help if Required/Requested
 rem === Updated On: 22 Jan 2022 / 30 Nov 2020
 rem ==========================================================================
:HelpMessage
 ECHO Assess and Validate File/Folder Integrity...
 ECHO:
 ECHO ----------
 ECHO YOU TYPED:  %0 %*
 ECHO ----------
 ECHO:
 ECHO -----------
 ECHO CMD SYNTAX:
 ECHO -----------
 ECHO  %~n0  [[drive:] [drive:] ...]  [OPTIONS]
 ECHO  %~n0  -h^|-?^|-h2^|-??
 ECHO:
 ECHO -------------------
 ECHO OPTION DEFINITIONS:
 ECHO -------------------
 ECHO  -?,  /H ..... Display This Help Message  (also --HELP)
 ECHO  -??, /H2 .... Display Extended Help Message
 ECHO        .  .... Schedule the Job Only
 ECHO        -  .... Unschedule the Job Only, Without Executing It First
 ECHO        @  .... Execute the Job, But Do Not Schedule It
 ECHO        $  .... Auto-Discover valid drives upon each scheduled run
 ECHO        ~  .... Enable Extended Configuration for System Integrity Checking
 ECHO        +  .... Send Email Regardless of Change Status
 ECHO   #:hash  .... Specify Valid Encryption Algorithm from below   {DEFAULT = SHA256}
 ECHO:
 ECHO ----------------------
 ECHO VALID HASH ALGORITHMS:
 ECHO ----------------------
 ECHO  * Secure Cryptographic Algorithms ..... SHA256, SHA384, SHA512, SHA3, BLAKE2B, BLAKE2S, BLAKE3
 ECHO  * InSecure Cryptographic Algorithms ... SHA1, MD5
 ECHO  * Non-Cryptographic Algorithms ........ XXHash64, MurmurHash3_X64_128, MurmurHash3_X86_128
 ECHO:

 IF ERRORLEVEL 8 IF NOT ERRORLEVEL 16 (
	 ECHO ----------------
	 ECHO SYNTAX EXAMPLES:
	 ECHO ----------------
	 ECHO  %~n0
	 ECHO  %~n0 C:
	 ECHO  %~n0 C: F: .
	 ECHO  %~n0 G: @
	 ECHO  %~n0 D: ~
	 ECHO  %~n0 $ #:BLAKE3
	 ECHO  %~n0 $ @
	 ECHO  %~n0 -
	 ECHO  %~n0 -?
	 ECHO:
	 ECHO ------------
	 ECHO USAGE NOTES:
	 ECHO ------------
	 ECHO  * If no drive is selected, the script will auto detect all valid drives
	 ECHO    ^(local fixed drives^) and set them all for scanning.  This is the same
	 ECHO    selecting the "$" parameter.  If one or more drives is selected, then
	 ECHO    only those selected drives which are also valid fixed drives will
	 ECHO    scanned.  Custom drive exemptions can be set via the following INPUT
	 ECHO    files: CustomVariables.TXT -OR- CustomVariables.%COMPUTERNAME%.TXT
	 ECHO:
	 ECHO  * If an incorrect ^(but non-blank^) HASH option is selected, then the
	 ECHO    script will continue with XXHASH64.  Valid options include:  MD5,
	 ECHO    SHA1, SHA256, SHA384, SHA512, BLAKE2B, BLAKE2S, BLAKE3, and XXHASH64.
	 ECHO:
	 ECHO  * Unlike the SHAxxx and BLAKExx hashes, the XXHash64 is a generic,
	 ECHO    non-cryptographic hash built primarily for performance, not security.
	 ECHO    It is only advisable to use this hashing algorithm where the integrity
	 ECHO    verification not needed for regulatory purposes.
	 ECHO:
 )
 CALL :ShowStatus "FINISHED"
                                               